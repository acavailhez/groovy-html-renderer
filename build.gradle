allprojects {
    group 'acavailhez'

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

    group = 'com.github.acavailhez'
    version = '2.1'

    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java { srcDirs = [] } // everything compiled with groovy
            groovy { srcDirs = ["src"] }
        }
        test {
            java { srcDirs = ["test"] }
            groovy { srcDirs = ["test"] }
            resources { srcDirs = ["test-resources"] }
        }
    }

    // Add sources to the published artifact
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'groovy-html-renderer'
                    packaging 'jar'
                    description 'in-code html5 rendering engine leveraging the groovy syntax'
                    url 'https://github.com/acavailhez/groovy-html-renderer'

                    scm {
                        connection 'scm:git:git://github.com/acavailhez/groovy-html-renderer'
                        developerConnection 'scm:git:git@github.com:acavailhez/groovy-html-renderer'
                        url 'https://github.com/acavailhez/groovy-html-renderer'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'acavailhez'
                            name 'Arnaud CAVAILHEZ'
                            email 'arnaud@cavailhez.fr'
                        }
                    }
                }
            }
        }
    }
}

project(':core') {

    archivesBaseName = 'groovy-html-renderer'

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.4.6'
        compile 'log4j:log4j:1.2.17'
        compile 'org.apache.commons:commons-lang3:3.0'
        compile 'org.jsoup:jsoup:1.8.3' // for html prettyfier
        compile 'org.apache.commons:commons-lang3:3.4'

        testCompile project(':tests')
        testCompile project(':bootstrap')
        testCompile 'org.apache.velocity:velocity:1.7' // for perf comparison
        testCompile 'org.apache.velocity:velocity-tools:2.0'

    }
}

project(':bootstrap') {

    archivesBaseName = 'groovy-html-renderer-bootstrap'

    dependencies {
        compile project(':core')

        testCompile project(':tests')
    }
}

project(':resources') {

    archivesBaseName = 'groovy-html-renderer-resources'

    dependencies {
        compile project(':core')

        testCompile project(':tests')
    }
}

project(':scripts') {

    dependencies {
        compile project(':core')
        compile project(':bootstrap')
        compile 'org.jodd:jodd:3.4.1'
        compile 'org.jodd:jodd-http:3.7'

        testCompile project(':tests')
    }
}

project(':jetty') {

    apply plugin: 'jetty'

    dependencies {
        compile project(':core')
        compile 'org.eclipse.jetty:jetty-server:9.3.6.v20151106'

        testCompile project(':tests')
    }
}

project(':jetty-demo') {

    apply plugin: 'jetty'

    dependencies {
        compile project(':jetty')

        testCompile project(':tests')
    }
}

project(':tests') {

    dependencies {
        compile project(':core')

        compile 'junit:junit:4.11'
    }
}